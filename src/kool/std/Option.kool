package kool.std

class Option<T> {

	var isDefined: Bool
	var value: T

	var static none = new Option<T>()

	Def static Some(value: T) = return new Option<T>(value)
	Def static None() = return none

	Def new() = {}

	Def implicit new(value: T) = Add(value)

	Def ==(lhs: Option<T>, rhs: Option<T>) = {
		if(lhs.isDefined && rhs.isDefined)
			return lhs.value == rhs.value

		if(!lhs.isDefined && !rhs.isDefined)
			return true

		return false
	}

	Def !=(lhs: Option<T>, rhs: Option<T>) = return !(lhs == rhs)

	Def #(option: Option<T>) = return option.isDefined ? #option.value : 0

	Def Add(value: T) = {
		this.value = value
		isDefined = true
	}

	Def IsDefined() = return isDefined

	Def Value() = {
		if(!isDefined)
			error("Cannot access value of undefined option.")
		return value
	}

	Def toString() = return isDefined ? "Some(" + value + ")" : "None"


}